// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:

    //Determine if A instruction or C instruction
    Not(in=instruction[15], out=Ainstruction);
    Not(in=Ainstruction, out=Cinstruction);

    //Check if C instruction and should go to A
    And(a=Cinstruction, b=instruction[5], out=CANDtoA);
    Mux16(a=instruction, b=ALUout, sel=CANDtoA, out=AregInput);

    //Check if A instruction or should go to A
    Or(a=Ainstruction, b=CANDtoA, out=loadA);
    ARegister(in=AregInput, load=loadA, out=AregOut, out[0..14]=addressM);

    //Select A or M
    Mux16(a=AregOut, b=inM, sel=instruction[12], out=AorM);

    //Check if C instruction and loading D then Load the D register
    And(a=Cinstruction, b=instruction[4], out=loadD);
    DRegister(in=ALUout, load=loadD, out=Dout); 

    //Calculate instruction
    ALU(x=Dout, y=AorM, zx=instruction[11], nx=instruction[10], 
        zy=instruction[9], ny=instruction[8], f=instruction[7],
        no=instruction[6], out=ALUout, out=outM, zr=zrout, ng=ngout);

    //Check if we are writing to M
    And(a=Cinstruction, b=instruction[3], out=writeM);

    //Decode the jump instruction
    And(a=zrout, b=instruction[1], out=JEQ);
    And(a=ngout, b=instruction[2], out=JLT);
    Or(a=zrout, b=ngout, out=zeroOrNeg);
    Not(in=zeroOrNeg, out=positive);
    And(a=positive, b=instruction[0], out=JGT);
    Or(a=JEQ, b=JLT, out=JLE);
    Or(a=JLE, b=JGT, out=jumpToA);
    And(a=Cinstruction, b=jumpToA, out=PCload);
    Not(in=PCload, out=PCinc);

    //Jump or increment
    PC(in=AregOut, reset=reset, load=PCload, inc=PCinc, out[0..14]=pc);

}